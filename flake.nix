{
  description = "naito's nix-darwin system flake";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";

    nix-darwin.url = "github:LnL7/nix-darwin";
    nix-darwin.inputs.nixpkgs.follows = "nixpkgs";

    home-manager.url = "github:nix-community/home-manager";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";

    treefmt-nix.url = "github:numtide/treefmt-nix";
    treefmt-nix.inputs.nixpkgs.follows = "nixpkgs";

    nix-homebrew.url = "github:zhaofengli/nix-homebrew";

    # brew bundle unconditionally loads the core tap when
    # HOMEBREW_NO_INSTALL_FROM_API is set, so it must be pinned even though
    # no formulae are installed via Homebrew.
    homebrew-core = {
      url = "github:homebrew/homebrew-core";
      flake = false;
    };

    homebrew-cask = {
      url = "github:homebrew/homebrew-cask";
      flake = false;
    };

    homebrew-productdevbook = {
      url = "github:productdevbook/homebrew-tap";
      flake = false;
    };

    vscode-settings.url = "github:naitokosuke/vscode-settings";
    vscode-settings.flake = false;

    # Claude Code skills - non-flake input; home/claude.nix readDirs the
    # locked snapshot to discover skill names in pure eval.
    # Resync after adding/removing a skill: push it, then
    #   nix flake update skill-skill-skill
    skill-skill-skill = {
      url = "github:naitokosuke/skill-skill-skill";
      flake = false;
    };

    # TODO: Once the herdr Darwin build fix (NixOS/nixpkgs#536015) lands in
    # nixpkgs-unstable, switch back to pkgs.herdr and delete this input.
    herdr.url = "github:ogulcancelik/herdr";
    herdr.inputs.nixpkgs.follows = "nixpkgs";

    llm-agents.url = "github:numtide/llm-agents.nix";
    llm-agents.inputs.nixpkgs.follows = "nixpkgs";

    mcp-servers-nix.url = "github:natsukium/mcp-servers-nix";
    mcp-servers-nix.inputs.nixpkgs.follows = "nixpkgs";

    nu-scripts = {
      url = "github:nushell/nu_scripts";
      flake = false;
    };
  };

  outputs =
    inputs@{
      nixpkgs,
      nix-darwin,
      home-manager,
      treefmt-nix,
      nix-homebrew,
      llm-agents,
      ...
    }:
    let
      system = "aarch64-darwin";
      pkgs = import nixpkgs {
        localSystem = system;
      };

      hosts = [
        "Mac-big"
        "Macbook-heavy"
      ];

      mkDarwinConfig =
        hostName:
        nix-darwin.lib.darwinSystem {
          specialArgs = {
            inherit inputs;
          };
          modules = [
            ./modules/naitokosuke
            (
              { config, ... }:
              {
                networking.hostName = hostName;
                networking.computerName = hostName;
                system.primaryUser = config.naitokosuke.username;
                nixpkgs.config.allowUnfree = true;
                nixpkgs.hostPlatform = system;
                nixpkgs.overlays = [
                  # Custom packages tracked by nvfetcher (./pkgs, issue #342)
                  (final: _: import ./pkgs { pkgs = final; })
                  llm-agents.overlays.shared-nixpkgs
                  # TODO: Remove after nixpkgs fixes nushell test failures in sandbox
                  # https://github.com/NixOS/nixpkgs/issues (nushell 0.112.1 SHLVL tests fail with "Operation not permitted")
                  (final: prev: {
                    nushell = prev.nushell.overrideAttrs (old: {
                      doCheck = false;
                    });
                  })
                  # FIXME: Remove once nixpkgs ships a fix for direnv checkPhase hang on Darwin.
                  # cache.nixos.org serves fish/zsh binaries with broken code signatures, so
                  # macOS Gatekeeper SIGKILLs them during `zsh ./test/direnv-test.zsh`, causing
                  # the build to hang indefinitely.
                  # https://github.com/NixOS/nixpkgs/issues/513019
                  # https://github.com/NixOS/nixpkgs/pull/513081 (proposed fix, not merged)
                  (final: prev: {
                    direnv = prev.direnv.overrideAttrs (old: {
                      doCheck = false;
                    });
                  })
                ];
              }
            )
            home-manager.darwinModules.home-manager
            nix-homebrew.darwinModules.nix-homebrew
            ./hosts/common
            ./hosts/${hostName}
          ];
        };
    in
    {
      darwinConfigurations = nixpkgs.lib.genAttrs hosts mkDarwinConfig;

      packages.${system} = import ./pkgs { inherit pkgs; };

      formatter.${system} = treefmt-nix.lib.mkWrapper pkgs {
        projectRootFile = "flake.nix";
        programs.nixfmt.enable = true;
      };
    };
}
